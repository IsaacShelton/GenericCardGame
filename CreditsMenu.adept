
import 'main.adept'

struct CreditsMenu (
    is_back_button_hovered bool
) {
    func setup {
        this.is_back_button_hovered = false
    }
    
    func enter {}
    func exit {}
    
    func __defer__ {
        // Due to the compiler not yet automatically generating __defer__ functions
        // via func& lookup, we have to manually ensure __defer__ is generated
    }
    
    func step {
        captMouseViewPosition(undef mouseX float, undef mouseY float)
        this.is_back_button_hovered = isButtonHovered(this, func &getBackButtonPosition(*CreditsMenu, *float, *float), mouseX, mouseY)
    }
    
    func click(mouseX, mouseY float, button int){
        unless button == 1, return
        
        this.is_back_button_hovered = isButtonHovered(this, func &getBackButtonPosition(*CreditsMenu, *float, *float), mouseX, mouseY)
        if this.is_back_button_hovered {
            gamedata.setScene('optionsmenu')
            sfx.play(sfx.back)
            return
        }
    }
    
    func draw {
        captMouseViewPosition(undef mouseX float, undef mouseY float)
        
        #if enable_credits_hd_hover
        andy_distance  float = distance(mouseX, mouseY, captViewWidth() / 2.0f - 128.0f, captViewHeight() / 2.0f)
        isaac_distance float = distance(mouseX, mouseY, captViewWidth() / 2.0f + 128.0f, captViewHeight() / 2.0f)
        #else
        andy_distance  float = 0.0f
        isaac_distance float = 0.0f
        #end
        
        drawTextCentered("andy possis", captViewWidth() / 2.0f - 128.0f, captViewHeight() / 2.0f - 64.0f)
        drawTextCentered("head artist", captViewWidth() / 2.0f - 128.0f, captViewHeight() / 2.0f + 64.0f)
        drawCentered(andy_distance < 32.0f ? textures.andyhd : textures.andyhd, captViewWidth() / 2.0f - 128.0f, captViewHeight() / 2.0f, 64.0f, 64.0f)
        drawCentered(textures.author, captViewWidth() / 2.0f - 128.0f, captViewHeight() / 2.0f, 64.0f, 64.0f)
        
        drawTextCentered("isaac shelton", captViewWidth() / 2.0f + 128.0f, captViewHeight() / 2.0f - 64.0f)
        drawTextCentered("lead developer", captViewWidth() / 2.0f + 128.0f, captViewHeight() / 2.0f + 64.0f)
        drawCentered(isaac_distance < 32.0f ? textures.isaachd : textures.isaachd, captViewWidth() / 2.0f + 128.0f, captViewHeight() / 2.0f, 64.0f, 64.0f)
        drawCentered(textures.author, captViewWidth() / 2.0f + 128.0f, captViewHeight() / 2.0f, 64.0f, 64.0f)
        
        this.getBackButtonPosition(undef back_x float, undef back_y float)
        drawButton("back", back_x, back_y, this.is_back_button_hovered)
    }
    
    func getBackButtonPosition(out x, y *float){
        *x = 16.0f
        *y = captViewHeight() - 16.0f - MAIN_MENU_BUTTON_HEIGHT
    }
}


import 'main.adept'

struct OptionsMenu (
    buttons <String> List,
    actions <func() void> List,
    hovered_button int,
    is_back_button_hovered bool
) {
    func setup {
        this.buttons.add("profile")
        this.buttons.add("extras")
        this.buttons.add("grfx")
        this.buttons.add("music")
        this.buttons.add("sfx")
        this.buttons.add("credits")
        this.actions.add(func &doOptionsProfile)
        this.actions.add(func &doOptionsExtras)
        this.actions.add(func &doOptionsGRFX)
        this.actions.add(func &doOptionsMusic)
        this.actions.add(func &doOptionsSFX)
        this.actions.add(func &doOptionsCredits)
        this.hovered_button = 0 // 0 == none
        this.is_back_button_hovered = false
    }
    
    func enter {}
    func exit {}
    
    func __defer__ {
        // Due to the compiler not yet automatically generating __defer__ functions
        // via func& lookup, we have to manually ensure __defer__ is generated
    }
    
    func step {
        this.hovered_button = 0
        captMouseViewPosition(undef mouseX float, undef mouseY float)
        
        repeat static this.buttons.length {
            if isMouseOverOptionsMenuButton(mouseX, mouseY, idx as int + 1) {
                this.hovered_button = idx as int + 1
            }
        }
        
        this.is_back_button_hovered = isButtonHovered(this, func &getBackButtonPosition(*OptionsMenu, *float, *float), mouseX, mouseY)
    }
    
    func click(mouseX, mouseY float, button int){
        unless button == 1, return
        
        repeat static this.actions.length {
            if isMouseOverOptionsMenuButton(mouseX, mouseY, idx as int + 1) {
                action func() void = this.actions.get(idx)
                if action as ptr, action()
                sfx.play(sfx.button)
                return
            }
        }
        
        this.is_back_button_hovered = isButtonHovered(this, func &getBackButtonPosition(*OptionsMenu, *float, *float), mouseX, mouseY)
        if this.is_back_button_hovered {
            gamedata.setScene('mainmenu')
            sfx.play(sfx.back)
            return
        }
    }
    
    func draw {
        each button_text String in this.buttons {
            getOptionsMenuButtonPosition(idx + 1, undef button_x float, undef button_y float)
            drawButton(button_text, button_x, button_y, this.hovered_button == idx + 1)
        }
        
        this.getBackButtonPosition(undef back_x float, undef back_y float)
        drawButton("back", back_x, back_y, this.is_back_button_hovered)
    }
    
    func getBackButtonPosition(out x, y *float){
        *x = 16.0f
        *y = captViewHeight() - 16.0f - MAIN_MENU_BUTTON_HEIGHT
    }
}

func getOptionsMenuButtonPosition(button int, out x, y *float){
    // NOTE: 'button' starts at 1
    
    middle_x float = captViewWidth() / 2.0f
    middle_y float = captViewHeight() / 2.0f
    y_starting_offset float = 128.0f
    y_run float = MAIN_MENU_BUTTON_HEIGHT + 16.0f
    
    x_start float = middle_x - MAIN_MENU_BUTTON_WIDTH / 2.0f
    y_start float = middle_y - y_starting_offset
    
    y_start += y_run * (button - 1) as float
    
    *x = x_start
    *y = y_start
}

func isMouseOverOptionsMenuButton(mouse_x, mouse_y float, button int) bool {
    getOptionsMenuButtonPosition(button, undef x float, undef y float)
    
    return mouse_x > x && mouse_x < x + MAIN_MENU_BUTTON_WIDTH && 
        mouse_y > y && mouse_y < y + MAIN_MENU_BUTTON_HEIGHT
}

func doOptionsProfile {
    gamedata.setScene('profilemenu')
}

func doOptionsExtras {
    gamedata.setScene('extrasmenu')
}

func doOptionsGRFX {
    gamedata.setScene('grfxmenu')
}

func doOptionsMusic {
    gamedata.setScene('musicmenu')
}

func doOptionsSFX {
    gamedata.setScene('sfxmenu')
}

func doOptionsCredits {
    gamedata.setScene('creditsmenu')
}

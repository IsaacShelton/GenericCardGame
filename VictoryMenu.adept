
import 'main.adept'

struct VictoryMenu (
    is_ok_button_hovered bool
) {
    func setup {}
    func enter {}
    func exit {}
    
    func __defer__ {
        // Due to the compiler not yet automatically generating __defer__ functions
        // via func& lookup, we have to manually ensure __defer__ is generated
    }
    
    func step {
        captMouseViewPosition(undef mouseX float, undef mouseY float)
        this.calculateWhetherOkButtonIsHovered(mouseX, mouseY)
    }
    
    func click(x, y float, button int){
        unless button == 1, return
        
        if this.calculateWhetherOkButtonIsHovered(x, y) {
            gamedata.setScene('mainmenu')
            soundtrack.fadeOutIntoNextSong()
            return
        }
    }
    
    func char(character uint) {}
    
    func key(key, scancode, action, mods int) {}
    
    func draw {
        drawButton("ok", this, func &getOkButtonPosition(*VictoryMenu, *float, *float), this.is_ok_button_hovered)
        
        notice_message String = gamedata.manager.getIsOnline() ? "Victory!" : "You are Offline"
        
        unless notice_message.empty() {
            drawTextCentered(notice_message)
            return
        }
    }
    
    func getOkButtonPosition(out x, y *float){
        *x = 16.0f
        *y = captViewHeight() - 16.0f - MAIN_MENU_BUTTON_HEIGHT
    }
    
    func calculateWhetherOkButtonIsHovered(mouseX, mouseY float) bool {
        this.is_ok_button_hovered = isButtonHovered(this, func &getOkButtonPosition(*VictoryMenu, *float, *float), mouseX, mouseY)
        return this.is_ok_button_hovered
    }
}

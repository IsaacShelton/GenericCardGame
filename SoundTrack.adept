
import 'main.adept'

SOUND_TRACK_COUNT == 8

struct SoundTrack (
    tracks <Sound> List,
    mixer SoundMixer,
    current, count int,
    fading_out_into bool,
    wait_frames int,
    is_paused bool
) {
    func init(loadedOneSongCallback func(ptr) void, user_pointer ptr) {
        music_folder String = where() + "assets/music/"
        tracks_count usize = SOUND_TRACK_COUNT
        
        #unless no_music
        repeat tracks_count {
            song *Sound = this.tracks.add()
            song.load(music_folder + "%.ogg" % (idx + 1), false, 0.15f)
            
            if loadedOneSongCallback, loadedOneSongCallback(user_pointer)
        }
        #end
        
        this.mixer.create()
        this.count = tracks_count
        
        this.fading_out_into = false
        this.wait_frames = 0
        this.is_paused = false
    }
    
    func start {
        #unless no_music
            this.current = 1
            this.mixer.play(this.tracks.get(0))
        #end
    }
    
    func update {
        #unless no_music
            this.mixer.update()
            
            if this.fading_out_into {
                if this.mixer.isSilent() {
                    this.wait_frames = 60
                    this.fading_out_into = false
                }
                return
            }
            
            if this.wait_frames > 0 && --this.wait_frames == 0 {
                this.nextSong()
            }
            
            this.maybeNextSong()
        #end
    }
    
    func maybeNextSong {
        #unless no_music
            unless this.mixer.isPlaying() || this.is_paused, this.nextSong()
        #end
    }
    
    func nextSong {
        #unless no_music
            if this.is_paused, return
            if this.current++ == this.count, this.current = 1
            this.playTrack(this.current)
        #end
    }
    
    func fadeOutIntoNextSong {
        #unless no_music
            if this.is_paused, return
            this.mixer.fadeOut()
            this.fading_out_into = true
        #end
    }
    
    func playTrack(track_id int) {
        #unless no_music
            if this.is_paused, return
            this.mixer.play(this.tracks.get(track_id - 1))
        #end
    }
    
    func toggle {
        #unless no_music
            if this.is_paused {
                this.mixer.play()
            } else {
                this.mixer.pause()
            }
            
            this.is_paused = !this.is_paused
        #end
    }
    
    func stop {
        #unless no_music
            this.mixer.stop()
        #end
    }
    
    func setVolumeAmplifier(amplifier float) {
        this.mixer.setVolumeAmplifier(amplifier)
    }
    
    func destroy {
        this.mixer.stop()
        this.mixer.destroy()
        
        each Sound in this.tracks, it.destroy()
    }
}

soundtrack SoundTrack
